import java.nio.file.Files

apply plugin: 'groovy'

/*
 * Configure Project Versions
 */

boolean localRepo = project.getProperties().containsKey("localRepo")

Properties props = new Properties()
props.load(project.file('esh-version.properties').newDataInputStream())
String eshVersion = props.getProperty('eshadoop')
String esVersion = props.getProperty('elasticsearch')

// determine if we're building a prerelease or candidate (alphaX/betaX/rcX)
String qualifier = System.getProperty("build.version_qualifier", "")
if (qualifier.isEmpty() == false) {
    if (qualifier.matches("(alpha|beta|rc)\\d+") == false) {
        throw new IllegalStateException("Invalid qualifier: " + qualifier)
    }
    eshVersion += "-" + qualifier
    esVersion += "-" + qualifier
}

def currentBranch = System.properties[ 'build.vcs.branch' ]
def buildNumber = System.getProperty( 'build.number', 'dev' ) // local dev or build server build
ext.isFinalRelease = Boolean.getBoolean( "${rootProject.name}.release.final" ) || currentBranch != null ? !currentBranch.contains('wip-') : false

// determine if we're building a snapshot or not (by default we will be)
if (!isFinalRelease) {
    // we update the version property to reflect if we are building a snapshot or a release build
    eshVersion += "-wip-${buildNumber}"
}

props.put("eshadoop", eshVersion)
props.put("elasticsearch", esVersion)

repositories {
    jcenter()
    mavenCentral()

    // For resolving propdeps (provided/optional dependencies)
    maven { url 'https://repo.spring.io/plugins-release' }

    // For Elasticsearch snapshots.
    if (localRepo) {
        // For some reason the root dirs all point to the buildSrc folder. The local Repo will be one above that.
        flatDir { dirs new File(project.rootDir, "../localRepo") }
    } else {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // Provided/Optional Dependencies
    compile 'org.springframework.build.gradle:propdeps-plugin:0.0.7'

    if (localRepo) {
        compile name: "build-tools-${esVersion}"
    } else {
        compile group: 'org.elasticsearch.gradle', name: 'build-tools', version: esVersion
    }
}

// write the updated properties to a temp property file
File tempPropertiesFile = new File(project.buildDir, "esh-version.properties")
task writeVersionProperties {
    inputs.properties(props)
    outputs.file(tempPropertiesFile)
    doLast {
        OutputStream stream = Files.newOutputStream(tempPropertiesFile.toPath())
        try {
            props.store(stream, "UTF-8")
        } finally {
            stream.close()
        }
    }
}

// copy the saved property file to the resources dir
processResources {
    dependsOn writeVersionProperties
    from tempPropertiesFile
}
